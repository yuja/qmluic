cmake_minimum_required(VERSION 3.12)

project(examples VERSION 0.1 LANGUAGES CXX)

set(CMAKE_INCLUDE_CURRENT_DIR ON)

# AUTOUIC doesn't work since .ui will be generated by qmluic.
# DO NOT ENABLE: set(CMAKE_AUTOUIC ON)
set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTORCC ON)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# Help Qt Creator find our type stub
set(QML_IMPORT_PATH ${CMAKE_CURRENT_SOURCE_DIR}/../contrib/imports CACHE STRING "" FORCE)

option(USE_QT6 "Use Qt6 even if Qt5 found")
if(USE_QT6)
  find_package(Qt6 REQUIRED COMPONENTS Widgets)
else()
  find_package(Qt5 5.15 COMPONENTS Widgets)
  if(NOT Qt5_FOUND)
    find_package(Qt6 REQUIRED COMPONENTS Widgets)
  endif()
endif()

# TODO: extract reusable part as cmake module
set(QMLUIC_COMMAND cargo run --)
function(target_qmlui_sources target)
  set(qml_files ${ARGN})
  list(TRANSFORM qml_files PREPEND "${CMAKE_CURRENT_SOURCE_DIR}/" OUTPUT_VARIABLE abs_qml_files)
  list(TRANSFORM qml_files TOLOWER OUTPUT_VARIABLE ui_files)
  list(TRANSFORM ui_files REPLACE "\.qml$" ".ui")
  list(TRANSFORM ui_files PREPEND "${CMAKE_CURRENT_SOURCE_DIR}/" OUTPUT_VARIABLE abs_ui_files)

  target_sources(${target} PRIVATE ${qml_files})
  add_custom_command(
    OUTPUT ${abs_ui_files}
    COMMAND ${QMLUIC_COMMAND} generate-ui -- ${qml_files}
    DEPENDS ${abs_qml_files}
    WORKING_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}"
    COMMENT "Generating UI from QML"
  )

  qt_wrap_ui(header_files ${ui_files})
  target_sources(${target} PRIVATE ${header_files})
endfunction()

add_subdirectory(customwidget)

add_executable(qmluic-examples
  hgemaildialog.cpp
  hgemaildialog.h
  itemviews.cpp
  itemviews.h
  layoutflow.cpp
  layoutflow.h
  main.cpp
  mainwindow.cpp
  mainwindow.h
  settingsdialog.cpp
  settingsdialog.h
  staticitemmodel.cpp
  staticitemmodel.h
  variouslayouts.cpp
  variouslayouts.h
)

target_qmlui_sources(qmluic-examples
  HgEmailDialog.qml
  ItemViews.qml
  LayoutFlow.qml
  MainWindow.qml
  SettingsDialog.qml
  StaticItemModel.qml
  VariousLayouts.qml
)

target_compile_definitions(qmluic-examples
  PRIVATE
    # The following define makes your compiler emit warnings if you use
    # any feature of Qt which has been marked as deprecated
    QT_DEPRECATED_WARNINGS
    # disables all the APIs deprecated before Qt 6.0.0
    QT_DISABLE_DEPRECATED_BEFORE=0x060000
    # disables erroneous QString cast, but allows QString("...")
    QT_RESTRICTED_CAST_FROM_ASCII
)

target_link_libraries(qmluic-examples
  PRIVATE
    Qt::Widgets
)
