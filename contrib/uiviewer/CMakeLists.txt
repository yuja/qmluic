cmake_minimum_required(VERSION 3.12)

project(uiviewer VERSION 0.1 LANGUAGES CXX)

list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/../../cmake")
include(QmluicMacros)

set(CMAKE_INCLUDE_CURRENT_DIR ON)

# AUTOUIC doesn't work since .ui will be generated by qmluic.
# DO NOT ENABLE: set(CMAKE_AUTOUIC ON)
set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTORCC ON)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# Help Qt Creator find our type stub
set(QML_IMPORT_PATH ${CMAKE_CURRENT_SOURCE_DIR}/../imports CACHE STRING "" FORCE)

# Use the up-to-date version of the qmluic command.
set(QMLUIC_COMMAND cargo run --)

option(USE_QT6 "Use Qt6 even if Qt5 found")
if(USE_QT6)
  find_package(Qt6 REQUIRED COMPONENTS UiTools Widgets)
else()
  find_package(Qt5 5.15 COMPONENTS UiTools Widgets)
  if(NOT Qt5_FOUND)
    find_package(Qt6 REQUIRED COMPONENTS UiTools Widgets)
  endif()
endif()

add_executable(qmluic-uiviewer
  main.cpp
  pipeserver.cpp
  pipeserver.h
  uiviewerdialog.cpp
  uiviewerdialog.h
)

qmluic_target_qml_sources(qmluic-uiviewer
  UiViewerDialog.qml
  OUTPUT_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
)

target_compile_definitions(qmluic-uiviewer
  PRIVATE
    # The following define makes your compiler emit warnings if you use
    # any feature of Qt which has been marked as deprecated
    QT_DEPRECATED_WARNINGS
    # disables all the APIs deprecated before Qt 6.0.0
    QT_DISABLE_DEPRECATED_BEFORE=0x060000
    # disables erroneous QString cast, but allows QString("...")
    QT_RESTRICTED_CAST_FROM_ASCII
)

target_link_libraries(qmluic-uiviewer
  PRIVATE
    Qt::UiTools
    Qt::Widgets
)

install(TARGETS qmluic-uiviewer)
